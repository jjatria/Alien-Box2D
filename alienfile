use alienfile;

use File::chdir;

configure {
  requires 'File::chdir' => 0;
};

plugin PkgConfig => 'box2d';

share {

  # Temporarily build from master, until 2.3.2 is released
  start_url 'https://github.com/erincatto/Box2D/archive/'
    . 'f655c603ba9d83f07fc566d38d2654ba35739102.zip';
  plugin 'Download';
  plugin 'Extract' => 'zip';

  # This will be used when Box2D releases its next version
  #
  # start_url 'https://github.com/erincatto/Box2D/releases';
  #
  # plugin 'Download' => (
  #   version => qr/v([0-9\.]+)/,
  #   filter  => qr/\.tar\.gz$/,
  # );
  #
  # plugin 'Decode::HTML';
  # plugin 'Extract' => 'tar.gz';

  plugin 'Build::Premake5';

  build sub {
    my ($build) = @_;

    local $CWD;
    my $extract = Path::Tiny::path($build->install_prop->{extract});

    $CWD = $extract->child('Box2D')->stringify;
    $build->system('%{premake5}', 'gmake');

    $CWD = $extract->child(qw( Box2D Build gmake ))->stringify;
    $build->system('%{make}', 'config=release');

    # Box2D makefiles do not define an install target
    _install($build);
  };
};

sub _install {
  my ($build) = @_;

  my $extract = Path::Tiny::path($build->install_prop->{extract});
  my $install = Path::Tiny::path($build->install_prop->{prefix});

  foreach (
      [ 'lib'           => qr{\.a$}, qw( Box2D Build gmake bin Release )],
      [ 'include/Box2D' => qr{\.h$}, qw( Box2D Box2D                   )],
    ) {

    my ($dir, $regex, @parts) = @{$_};

    my $source = $extract->child(@parts);
    $source->visit(
      sub {
        if ($_->basename =~ $regex) {
          my $path = $_->relative($source);
          my $target = $install->child($dir, $path);
          $target->parent->mkpath;
          $_->copy($target);
        }
      },
      { recurse => 1 },
    );
  }

  my $file = $extract->child(qw( Box2D Box2D Common b2Settings.cpp ));

  my ($version) = grep { /version\s?=\s?\{[\d\s,]+\}/ } $file->lines;
  my @parts = $version =~ /([0-9]+)\s?[,\}]/g;
  $version = (@parts == 3) ? join '.', @parts : '';

  my $pkgconfig = $install->child(qw( lib pkgconfig box2d.pc ))->touchpath;
  my $fh = $pkgconfig->openw;

  print $fh 'prefix=' . $install                            . "\n";
  print $fh 'exec_prefix=${prefix}'                         . "\n";
  print $fh 'includedir=${prefix}/include'                  . "\n";
  print $fh 'libdir=${prefix}/lib'                          . "\n";
  print $fh "\n";
  print $fh 'Name: Box2D'                                   . "\n";
  print $fh 'Description: 2D physics engine'                . "\n";
  print $fh 'Version: ' . $version                          . "\n";
  print $fh 'Cflags: -I${includedir} -I${includedir}/Box2D' . "\n";
  print $fh 'Libs: -L${libdir} -lBox2D'                     . "\n";
}
